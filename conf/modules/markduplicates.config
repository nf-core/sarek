/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
        ext.when   = When to run the module.
----------------------------------------------------------------------------------------
*/

// MARKDUPLICATES

process {

    withName: 'CRAM_TO_BAM' {
        ext.args   = { '-b' }
    }

    withName: 'BAM_TO_CRAM' {
        // BAM provided for step Markduplicates either run through MD or Convert -> then saved as sorted.cram (convert) or md.cram (md directly)
        // BAM files provided for step prepare_recal are converted and run through BQSR -> then saved as md.cram
        // BAM files provided for step recal are converted and run through BQSR II -> then saved as md.cram
        ext.args   = { '-C' }
        ext.prefix = { "${meta.id}.converted" }
    }

    withName: 'NFCORE_SAREK:SAREK:FASTQ_PREPROCESS_GATK:(BAM_MARKDUPLICATES|BAM_MARKDUPLICATES_SPARK):CRAM_QC_MOSDEPTH_SAMTOOLS:SAMTOOLS_STATS' {
        ext.prefix = { "${meta.id}.md.cram" }
        ext.when   = { !(params.skip_tools && params.skip_tools.split(',').contains('samtools')) }
    }

    withName: 'BAM_TO_CRAM_MAPPING' {
        ext.prefix = { "${meta.id}.sorted" }
        // Run only when mapping should be saved as CRAM or when no MD is done
        ext.when   = (params.save_mapped && !params.save_output_as_bam) ||
            (
                (params.skip_tools && params.skip_tools.split(',').contains('markduplicates')) &&
                !(params.tools && params.tools.split(',').contains('sentieon_dedup'))
            )
    }

    withName: 'GATK4_ESTIMATELIBRARYCOMPLEXITY' {
        ext.prefix = { "${meta.id}.md.cram" }
        ext.when   = { !(params.skip_tools && params.skip_tools.split(',').contains('markduplicates_report')) }
    }

    withName: 'GATK4_MARKDUPLICATES' {
        ext.args   = { [
            "--REMOVE_DUPLICATES false",
            "--VALIDATION_STRINGENCY LENIENT",
            params.markduplicates_pixel_distance ? "--OPTICAL_DUPLICATE_PIXEL_DISTANCE ${params.markduplicates_pixel_distance}" : '',
            params.umi_tag ? "--BARCODE_TAG ${umi_tag}" :
                (params.umi_in_read_header || params.umi_length ? "--BARCODE_TAG RX" : "")
        ].join(" ").trim() }
        ext.prefix = { "${meta.id}.md.cram" }
        ext.when   = { !(params.skip_tools && params.skip_tools.split(',').contains('markduplicates')) }
    }

    withName: 'GATK4SPARK_MARKDUPLICATES' {
        containerOptions = ''
        ext.args   = { '--remove-sequencing-duplicates false -VS LENIENT' }
        ext.prefix = { "${meta.id}.md.cram" }
    }

    withName: 'NFCORE_SAREK:SAREK:FASTQ_PREPROCESS_GATK:CRAM_TO_BAM' {
        ext.when   = { params.save_output_as_bam }
        if (params.tools && params.tools.split(',').contains('sentieon_dedup')) {
            ext.prefix = { "${meta.id}.dedup" }
        } else {
            ext.prefix = { "${meta.id}.md" }
        }
    }
}
